ZZDGPMSE ;Unit Tests - Clinic API; 2/11/2013
 ;;1.0;UNIT TEST;;05/28/2012;
ADDCLN(NAME) ; Add new clinic
 N IEN
 S IEN=$P(^SC(0),U,3)+1
 S STOP=$O(^DIC(40.7,0))
 S ^SC("B",NAME,IEN)=""
 S $P(^SC(0),U,3)=$P(^SC(0),U,3)+1
 S ^SC(IEN,0)=NAME_"^^C^^^^"_STOP
 S $P(^SC(IEN,0),U,18)=$O(^DIC(40.7,STOP))
 S $P(^SC(IEN,0),U,17)="Y"
 S ^SC(IEN,"SL")="30^V^^^^4^1^Y"
 S ^SC(IEN,"AT")="9"
 S ^SC(IEN,"SDPRIV",0)="^44.04PA^1^1"
 S ^SC(IEN,"SDP")="5^2^3^"
 Q IEN_U_NAME
 ;
ADDPATT(SC) ;
 S DD=9999999
 F I=0:1:6 S ^SC(SC,"T"_I,DD,0)=DD,^SC(SC,"T"_I,DD,1)="[1 1 1 1|1 1 1 1|1 1 1 1|1 1 1 1|1 1 1 1|1 1 1 1|1 1 1 1|1 1 1 1]"
 Q
ADDPAT(NAME,I) ; Add new patient
 N IEN
 S IEN=$P(^DPT(0),U,3)+I
 S ^DPT("B",NAME_I,IEN)=""
 S ^DPT(IEN,0)=NAME_I_"^M^2800621^^^^^^221133445^^^^^^1^3120628^^^^1"
 Q IEN_U_NAME_I
 ;
SETUP(PNM,CNM) ;
 S ^DIC(213.9,1,0)="1^1"
 S ^DIC(213.9,1,"OFF")=1
 S $P(^ORD(101,1380,0),"^",3)=""
 S:$D(PNM) PNAME=PNM
 S:$D(CNM) CNAME=CNM
 S:'$D(PNM) PNAME="TEST,PATIENT"
 S:'$D(CNM) CNAME="Test Clinic"
 S SC=$$ADDCLN(CNAME)
 S DFN=$$ADDPAT(PNAME,1)
 D ADDPATT(+SC)
 S SD=DT_".08",SD=SD_U_$$FMTE^XLFDT(SD)
 S RSN="Test Reason",LEN="30^30",TYPE="9^REGULAR",NXT="N"
 S CRSN="11^OTHER"
 D ADDWARDS
 Q
ADDWARDS()
 N WARD,PARA S PARA(.01)="Ward NHC 1",PARA(.03)="NH",PARA(44)=+SC,PARA(.017)=1
 D ADDWARD^ZZDGPMSE(.WARD,.PARA) S WARD1=WARD_U
 K PARA S PARA(400)=1.5 D UPDWARD^ZZDGPMSE(,.PARA,+WARD1)
 K WARD,PARA S PARA(.01)="Ward NHC 2",PARA(.03)="NH",PARA(44)=+SC,PARA(.017)=2
 D ADDWARD^ZZDGPMSE(.WARD,.PARA) S WARD2=+WARD_U
 K PARA S PARA(400)=1.6 D UPDWARD^ZZDGPMSE(,.PARA,+WARD2)
 K BED,PARA S PARA(.01)="R101-BED-1"
 D ADDBED^ZZDGPMSE(.BED,.PARA) S BED1=+BED_U
 K BED,PARA S PARA(.01)="R102-BED-2"
 D ADDBED^ZZDGPMSE(.BED,.PARA) S BED2=+BED_U
 Q
 ;
SETENR(DFN,SC) ; Set patient enrolls
 S ^DPT(+DFN,"DE","B",+SC,1)=""
 S ^DPT(+DFN,"DE",1,0)=+SC_"^"
 S ^DPT(+DFN,"DE",1,1,1,0)=DT_"^O^^^"
 Q
 ;
ADDWARD(RETURN,PARAMS) ; Add new ward
 D ADDNEW(.RETURN,42,.PARAMS)
 Q
 ;
ADDBED(RETURN,PARAMS) ; Add new bed
 D ADDNEW(.RETURN,405.4,.PARAMS)
 Q
ADDNEW(RETURN,FILE,PARAMS) ; Add entry to file
 N FLD,IENS,FDA
 S IENS="+1,"
 S FLD=0
 F  S FLD=$O(PARAMS(FLD)) Q:'FLD  D
 . S FDA(FILE,IENS,FLD)=PARAMS(FLD)
 D UPDATE^DIE("","FDA","IENS","RETURN")
 S RETURN=IENS(1)
 Q
 ;
ADDNEWS(RETURN,SFILE,IEN,PARAMS) ; Add entry to subfile
 N FLD,IENS,FDA
 S IENS="+1,"_IEN_","
 S FLD=0
 F  S FLD=$O(PARAMS(FLD)) Q:'FLD  D
 . S FDA(SFILE,IENS,FLD)=PARAMS(FLD)
 D UPDATE^DIE("","FDA","IENS","RETURN")
 S RETURN=IENS(1)
 Q
 ;
ADDWOOS(RETURN,WARD,PARAMS) ; Add ward out of service date
 D ADDNEWS(.RETURN,42.08,WARD,.PARAMS)
 Q
 ;
ADDFOOS(RETURN,FTS,PARAMS) ; Add FTS out of service date
 D ADDNEWS(.RETURN,45.702,+FTS,.PARAMS)
 Q
 ;
ADDBOOS(RETURN,BED,PARAMS) ; Add bed out of service date
 D ADDNEWS(.RETURN,405.42,BED,.PARAMS)
 Q
 ;
ADDBASW(RETURN,BED,PARAMS) ; Add ward which can assign
 D ADDNEWS(.RETURN,405.41,BED,.PARAMS)
 Q
 ;
UPDWARD(RETURN,PARAMS,IFN) ; Update ward
 D UPD(.RETURN,42,IFN,.PARAMS)
 Q
UPD(RETURN,FILE,IFN,PARAMS) ; Update ward
 N FLD,IENS,FDA
 S IENS=IFN_","
 S FLD=0
 F  S FLD=$O(PARAMS(FLD)) Q:'FLD  D
 . S FDA(FILE,IENS,FLD)=PARAMS(FLD)
 D FILE^DIE("","FDA","RETURN")
 S RETURN=IFN
 Q
 ;
