ZZDGPMAPI3 ;Unit Tests - Discharge API; 5/27/13
 ;;1.0;UNIT TEST;;05/28/2012;
 TSTART
 I $T(EN^XTMUNIT)'="" D EN^XTMUNIT("ZZDGPMAPI3")
 TROLLBACK
 Q
STARTUP ;
 S DTIME=500,DUZ=1,U="^"
 K XUSER,XOPT
 D LOGON^ZZRGUTCM
 S DT=$P($$HTFM^XLFDT($H),".")
 D SETUP^ZZDGPMSE()
 Q
 ;
SHUTDOWN ;
 Q
DSCH ;
 N DATE,AFN,DGQUIET
 S ADM("PATIENT")=DFN,ADM("TYPE")=1,ADM("ADMREG")=1,ADM("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-3)_U
 S ADM("FDEXC")=1,ADM("SHDIAG")="Transfer Admit diagnosis",ADM("WARD")=WARD1,ADM("FTSPEC")="1^",ADM("ATNDPHY")=DUZ
 S DATE=ADM("DATE") M ADM1=ADM
 S %=$$ADMIT^DGPMAPI1(.RT,.ADM) S AFN=+RT
 K PA,PAR,RE
 ;invalid admission
 S RTN="S %=$$DISCH^DGPMAPI3(.RE,.DIS)"
 S %=$$DISCH^DGPMAPI3(.RE,.DIS)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: INVPARM")
 D CHKEQ^XTMUNIT($P(RE(0),U,2)["'ADMIFN'",1,"Expected error: INVPARM")
 ; invalid param admission
 S DIS("ADMIFN")="not no",%=$$DISCH^DGPMAPI3(.RE,.DIS)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: INVPARM")
 D CHKEQ^XTMUNIT($P(RE(0),U),"INVPARM","Expected error: INVPARM")
 ; admission not found
 S DIS("ADMIFN")=AFN+300_U,%=$$DISCH^DGPMAPI3(.RE,.DIS)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: ADMNFND")
 D CHKEQ^XTMUNIT($P(RE(0),U),"ADMNFND","Expected error: ADMNFND")
 ;Check date
 S DIS("ADMIFN")=AFN_U
 D CHKDT^ZZDGPMUTL(RTN,.PAR)
 ;not before admission
 S DIS("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-3,-1)_U,%=$$DISCH^DGPMAPI3(.RE,.DIS)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: TRANBADM")
 D CHKEQ^XTMUNIT($P(RE(0),U),"TRANBADM","Expected error: TRANBADM")
 ;not before last movement
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-1)_U,PAR("ADMIFN")=AFN_U,PAR("TYPE")="18^"
 S %=$$TRANSF^DGPMAPI2(.RE,.PAR)
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-2)_U,%=$$DISCH^DGPMAPI3(.RE,.PAR)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: DCHNBLM")
 D CHKEQ^XTMUNIT($P(RE(0),U),"DCHNBLM","Expected error: DCHNBLM")
 ;invalid type
 S PAR("DATE")=$$NOW^XLFDT()_U,PAR("TYPE")="24^",PAR("INVTYPE")="1^",RTN="S %=$$DISCH^DGPMAPI3(.RE,.PAR)"
 D CHKTYPE^ZZDGPMSE(RTN,.PAR)
 ; invalid param facility
 S PAR("FCTY")="not no",%=$$DISCH^DGPMAPI3(.RE,.PAR)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: INVPARM")
 D CHKEQ^XTMUNIT($P(RE(0),U),"INVPARM","Expected error: INVPARM")
 ;ok
 K ZTQUEUED,DGQUIET
 S DIS("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,,-3)_U,DIS("TYPE")=24,DIS("ADMIFN")=AFN
 M DIS1=DIS
 S %=$$DISCH^DGPMAPI3(.RE,.DIS),DISCH=+RE
 D CHKEQ^XTMUNIT(RE>0,1,"Expected error: ASHWINVS")
 ;already discharged
 S %=$$DISCH^DGPMAPI3(.RE,.DIS1)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: DCHPADON")
 D CHKEQ^XTMUNIT($P(RE(0),U),"DCHPADON","Expected error: DCHPADON")
 ;cleaning up
 S ADM1("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,,-2)_U K ADM1("ROOMBED")
 S %=$$ADMIT^DGPMAPI1(.RE,.ADM1) S AFN1=+RE
 ;only last discharge
 S %=$$DELDSCH^DGPMAPI3(.RE,DISCH_U)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: DCHCDOLA")
 D CHKEQ^XTMUNIT($P(RE(0),U),"DCHCDOLA","Expected error: DCHCDOLA")
 ;
 S %=$$DELADM^DGPMAPI1(.RE,AFN1_U)
 S %=$$DELADM^DGPMAPI1(.RE,AFN_U)
 D CHKEQ^XTMUNIT(RE>0,1,"Unexpected error: "_$G(RE(0)))
 Q
FROMASIH ;
 N PAR,RE,DGQUIET,AFN
 S AFN=$$ADM()
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-1)_U,PAR("ADMIFN")=RT_U
 S PAR("ADMREG")="75^",PAR("ADMSRC")="53^",PAR("FDEXC")="0^",PAR("FTSPEC")="41^",PAR("PATIENT")=DFN,PAR("ATNDPHY")=DUZ_U
 S PAR("PRYMPHY")=DUZ_U,PAR("ROOMBED")=BED2_U,PAR("SHDIAG")="To asih diagnosis",PAR("TYPE")="13^",PAR("WARD")=WARD2_U
 S $P(^DIC(42,+WARD2,0),U,3)="M"
 S %=$$TRANSF^DGPMAPI2(.RE,.PAR)
 D CHKEQ^XTMUNIT(RE>0,1,"Expected error: ASHWINVS")
 ;discharge
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 S PAR("DATE")=+PAR("DATE")+0.0002,PAR("TYPE")=33,PAR("ADMIFN")=LMVT("ADMIFN")
 S %=$$DISCH^DGPMAPI3(.RE,.PAR),DISCH=+RE
 D CHKEQ^XTMUNIT(RE>0,1,"Unexpected error: "_$G(RE(0)))
 ;cleaning up
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 S %=$$DELTRA^DGPMAPI2(.RE,LMVT("MFN"))
 S %=$$DELDSCH^DGPMAPI3(.RE,DISCH)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: DCHCDWAH")
 D CHKEQ^XTMUNIT($P(RE(0),U),"DCHCDWAH","Expected error: DCHCDWAH")
 ; 
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 S %=$$DELADM^DGPMAPI1(.RE,LMVT("ADMIFN"))
 D CHKEQ^XTMUNIT(RE>0,1,"Unexpected error: "_$G(RE(0)))
 S %=$$DELADM^DGPMAPI1(.RE,AFN)
 D CHKEQ^XTMUNIT(RE>0,1,"Unexpected error: "_$G(RE(0)))
 Q
 ;
TRAOUT ;
 N PAR,AFN,DGQUIET
 S AFN=$$ADM()
 ;discharge
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-2.2)_U,PAR("TYPE")="28"_U,PAR("ADMIFN")=AFN
 S RTN="S %=$$DISCH^DGPMAPI3(.RE,.PAR)"
 D CHKFCTY^ZZDGPMAPI2(RTN,.PAR)
 S %=$$DISCH^DGPMAPI3(.RE,.PAR),DISCH=+RE
 D CHKEQ^XTMUNIT(RE>0,1,"Expected error: ASHWINVS")
 D CHKEQ^XTMUNIT(+$P(^DGPM(+DISCH,0),U,5),+$G(PAR("FCTY")),"Incorrect fcty - TRAOUT")
 S %=$$DELDSCH^DGPMAPI3(.RE,DISCH)
 D CHKEQ^XTMUNIT(RE>0,1,"Expected error: ASHWINVS")
 S %=$$DELADM^DGPMAPI1(.RE,AFN)
 D CHKEQ^XTMUNIT(RE>0,1,"Expected error: ASHWINVS")
 Q
 ;
TASH(AFN) ; Transfer to ASIH
 N PAR S PAR("ADMIFN")=AFN
 S PAR("ATNDPHY")=DUZ,PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-2),PAR("FTSPEC")="40^",PAR("FDEXC")="0^"
 S PAR("PATIENT")=DFN,PAR("SHDIAG")="To asih diagnosis",PAR("TYPE")=13,PAR("WARD")=WARD2,PAR("ADMREG")="75^"
 S %=$$TRANSF^DGPMAPI2(.RE,.PAR)
 Q RE
ADM() ;
 N ADM
 S ADM("PATIENT")=DFN,ADM("TYPE")=1,ADM("ADMREG")=1,ADM("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-3)_U
 S ADM("FDEXC")=1,ADM("SHDIAG")="Transfer Admit diagnosis",ADM("WARD")=WARD1,ADM("FTSPEC")="1^",ADM("ATNDPHY")=DUZ
 S %=$$ADMIT^DGPMAPI1(.RT,.ADM)
 Q RT_U
UPDDSCH ;Update discharge
 N PAR,AFN,DGQUIET
 S AFN=$$ADM()
 ;discharge
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-2)_U,PAR("TYPE")="24"_U,PAR("ADMIFN")=AFN
 S %=$$DISCH^DGPMAPI3(.RE,.PAR),DISCH=+RE_U
 ;Check date
 S RTN="S %=$$UPDDSCH^DGPMAPI3(.RE,.PAR,DISCH)"
 D CHKDT^ZZDGPMUTL(RTN,.PAR,1)
 ;not before last movement
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-4)_U,PAR("TYPE")="25"_U,PAR("ADMIFN")=AFN
 S %=$$UPDDSCH^DGPMAPI3(.RE,.PAR,DISCH)
 D CHKEQ^XTMUNIT(RE,0,"Expected error: DCHNBLM")
 D CHKEQ^XTMUNIT($P(RE(0),U),"DCHNBLM","Expected error: DCHNBLM")
 ;invalid type
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-1)_U,PAR("ADMIFN")=AFN
 S PAR("TYPE")="25"_U,PAR("INVTYPE")="23"_U
 D CHKTYPE^ZZDGPMSE(RTN,.PAR,1)
 ;ok to update
 K ZTQUEUED,DGQUIET
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-1)_U,PAR("TYPE")="25"_U,PAR("ADMIFN")=AFN
 S %=$$UPDDSCH^DGPMAPI3(.RE,.PAR,DISCH)
 D CHKEQ^XTMUNIT(RE,1,"Unexpected error: "_$G(RE(0)))
 S %=$$DELADM^DGPMAPI1(.RE,AFN)
 Q
CONTASH ; Continued ASIH
 N RE,PAR
 S AFN=$$ADM()
 S TFN=$$TASH(AFN)
 ; invalid parameter FCTY
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 S PAR("DATE")=$$FMADD^XLFDT($$NOW^XLFDT(),,-1)_U,PAR("TYPE")=35,PAR("ADMIFN")=LMVT("ADMIFN")
 S %=$$DISCH^DGPMAPI3(.RE,.PAR),DISCH=+RE
 D CHKEQ^XTMUNIT(RE,0,"Expected error: INVPARM")
 D CHKEQ^XTMUNIT($P(RE(0),U),"INVPARM","Expected error: INVPARM")
 ; discharge
 S FTS=$P(^DIC(4,0),U,3),PAR("FCTY")=FTS_U_$P(^DIC(4,FTS,0),U)
 S AFN2=^DGPM(LMVT("ADMIFN"),0)
 S %=$$DISCH^DGPMAPI3(.RE,.PAR),DISCH=+RE,$P(AFN2,U,17)=DISCH
 S DFN0=+PAR("DATE")_U_"3"_U_+DFN_"^35^^^^^^^^^^"_LMVT("ADMIFN")_"^^^^46^^^^1"
 D CHKEQ^XTMUNIT(AFN2,^DGPM(LMVT("ADMIFN"),0),"Incorrect admission node")
 D CHKEQ^XTMUNIT(DFN0,^DGPM(DISCH,0),"Incorrect discharge node")
 S TFN=$O(^DGPM("APTT2",26,+PAR("DATE")+0.0000002,0))
 S TFN0=+PAR("DATE")_U_"2"_U_+DFN_"^22^"_+PAR("FCTY")_"^^^^^^^^^"_AFN_"^^^45^^^^2"
 D CHKEQ^XTMUNIT(TFN0,^DGPM(TFN,0),"Incorrect transfer node")
 ; cannot delete while asih discharge
 S %=$$DELDSCH^DGPMAPI3(.RE,DISCH_U)
 D CHKEQ^XTMUNIT(RE_U_$P(RE(0),U),"0^DCHCDWAH","Expected error: DCHCDWAH")
 ; delete transfer (and discharge)
 S AFN2=^DGPM(LMVT("ADMIFN"),0)
 S %=$$DELTRA^DGPMAPI2(.RE,TFN),$P(AFN2,U,17)=""
 D CHKEQ^XTMUNIT(AFN2,^DGPM(LMVT("ADMIFN"),0),"Incorrect admission node")
 D CHKEQ^XTMUNIT(0,$D(^DGPM(DISCH,0)),"Incorrect discharge node")
 D CHKEQ^XTMUNIT(0,$D(^DGPM(TFN,0)),"Incorrect transfer node")
 Q
XTENT ;
 ;;DSCH;Discharge
 ;;FROMASIH;Discharge from asih.
 ;;TRAOUT;Transfer out
 ;;UPDDSCH;Update discharge
 ;;CONTASH;Continued ASIH
