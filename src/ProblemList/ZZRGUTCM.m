ZZRGUTCM ;RGI/CBR - Unit Tests - Common functions ;3/7/2013
 ;;1.0;UNIT TEST;;Apr 25, 2012;Build 1;
NEWPAT ;
 N PAT,PATU,PATDFN,ERR
 S PAT(.01)="GMPLTEST, PATIENT"
 S PAT(.02)="M"
 S PAT(.03)=2820226
 S PAT(.09)="000000121"
 S PAT(.096)=1
 S PAT(.097)=$P($$HTFM^XLFDT($H),".")
 S PAT(.301)="Y"
 S PAT(391)=1
 S PAT(1901)="Y"
 M PATU(2,"+1,")=PAT
 D UPDATE^DIE("","PATU","PATDFN","ERR")
 S GMPDFN=+$G(PATDFN(1))
 Q
 ;
NISTPRBS(PROB1,PROB2) ;
 K PROB1,PROB2
 S PROB1(.01)="9128^496"
 S PROB1(.02)=GMPDFN
 S PROB1(.05)="^Chronic Airway Obstruction, Not Elsewhere Classified"
 S PROB1(.12)="A^ACTIVE"
 S PROB1(.13)=3100404 ; April 4,2010
 S PROB1(1.01)=""
 S PROB1(1.02)="P^PERMANENT"
 S PROB1(1.08)=GMPCLIN
 S PROB1(1.14)="A^ACUTE"
 S PROB1(1.10)="0^NO"
 S PROB1(10,"NEW")=1
 S PROB1(10,"NEW",1)="testcomment3"
 S PROB2(.01)="2477^401.9"
 S PROB2(.02)=GMPDFN
 S PROB2(.05)="^Essential Hypertension"
 S PROB2(.12)="A^ACTIVE"
 S PROB2(.13)=3100330 ; March 30,2010
 S PROB2(1.01)=""
 S PROB2(1.02)="P^PERMANENT"
 S PROB2(1.08)=GMPCLIN
 S PROB2(1.14)="C^CHRONIC"
 S PROB2(1.10)="0^NO"
 S PROB2(10,"NEW")=1
 S PROB2(10,"NEW",1)="testcomment4"
 Q
 ;
CHKPRB(IFN,FIELDS,CONDT) ;
 N DBFLDS,FLDNO,R
 S FLDNO=""
 S R=$$DETAIL(.DBFLDS,IFN)
 F  S FLDNO=$O(FIELDS(FLDNO)) Q:FLDNO=""  D
 . I FLDNO=.05 D CHKEQ^XTMUNIT($P(FIELDS(FLDNO),U,2),$P(DBFLDS(FLDNO),U,2),"Invalid value in field: "_FLDNO) Q
 . I (FLDNO=1.01)&(FIELDS(1.01)="") D CHKEQ^XTMUNIT("1^Unresolved",DBFLDS(FLDNO),"Invalid value in field: "_FLDNO) Q
 . Q:FLDNO=10
 . D CHKEQ^XTMUNIT($P(FIELDS(FLDNO),U),$P(DBFLDS(FLDNO),U),"Invalid value in field: "_FLDNO)
 I $D(CONDT) D CHKEQ^XTMUNIT(CONDT,$P(DBFLDS(1.02),U))
 Q
 ;
CHKEDLD(RET) ;
 N ORG1,ORG2,FLD,I,IVAL,EVAL,ST
 D NISTPRBS(.ORG1,.ORG2)
 S I=""
 F  S I=$O(RET(I)) Q:I=""  D
 . S ST=$P(RET(I),$C(254))
 . S FLD=$P(RET(I),$C(254),2)
 . S IVAL=$P($P(RET(I),$C(254),3),U)
 . S EVAL=$P($P(RET(I),$C(254),3),U,2)
 . I "^.01^.02^.12^.13^1.02^1.08^1.14^1.1^"[("^"_FLD_"^") D CHKEQ^XTMUNIT($P($G(ORG1(FLD)),U),IVAL,"EDLOAD^ORQQPL1: Field "_FLD_" ("_ST_")")
 . I "^.05^"[("^"_FLD_"^") D CHKEQ^XTMUNIT($P($G(ORG1(FLD)),U,2),EVAL,"EDLOAD^ORQQPL1: Field "_FLD_" ("_ST_")")
 Q
 ;
SETARY(ARRIN,ARROUT) ;
 N I,ST,FLD,IVAL,EVAL
 K ARROUT
 S I=""
 F  S I=$O(RET(I)) Q:I=""  D
 . S ST=$P(RET(I),$C(254))
 . S FLD=$P(RET(I),$C(254),2)
 . S IVAL=$P($P(RET(I),$C(254),3),U)
 . S EVAL=$P($P(RET(I),$C(254),3),U,2)
 . I ST="NEW" S ARROUT(I)="GMPFLD("_FLD_")="""_IVAL_U_EVAL_""""
 . I ST="ORG" S ARROUT(I)="GMPORIG("_FLD_")="""_IVAL_U_EVAL_""""
 Q
 ;
ORARY(PROBS,ARY) ;
 N FLDNO,I
 K ARY
 S FLDNO="",I=1
 F  S FLDNO=$O(PROBS(FLDNO)) Q:FLDNO=""  D
 . Q:FLDNO=10
 . S ARY(I)="GMPFLD("_FLDNO_")="""_PROBS(FLDNO)_""""
 . S I=I+1
 I $D(PROBS(10,"NEW")) D
 . S ARY(I)="GMPFLD(10,""NEW"")="_PROBS(10,"NEW")
 . F FLDNO=1:1:PROBS(10,"NEW") D
 .. S ARY(I)="GMPFLD(10,""NEW"","_FLDNO_")="""_PROBS(10,"NEW",FLDNO)_""""
 Q
 ;
LOGON ;
 S DUZ=$$CHECKAV^XUSRB("fakedoc1;1Doc!@#$")
 D DUZ^XUS1A
 S GMPVAMC=DUZ(2)
 Q
 ;
DETAIL(RETURN,GMPIFN,GMPLMGR,GMPROV) ;
 N DIC,DIQ,DR,I,CNT,NIFN,FAC,EXT,FOUND
 S FOUND=$$DETAILF(GMPIFN,.RETURN,"")
 Q:'FOUND 0
 F I=.03,.08,.13,1.07,1.09 S $P(RETURN(I),U,2)=$$EXTDT^GMPLX($P(RETURN(I),U))
 S $P(RETURN(1.11),U,2)=$S($P(RETURN(1.11),U):"AGENT ORANGE",1:"")
 S $P(RETURN(1.12),U,2)=$S($P(RETURN(1.12),U):"RADIATION",1:"")
 S $P(RETURN(1.13),U,2)=$S($P(RETURN(1.13),U):"ENV CONTAMINANTS",1:"")
 S $P(RETURN(1.15),U,2)=$S($P(RETURN(1.15),U):"HEAD/NECK CANCER",1:"")
 S $P(RETURN(1.16),U,2)=$S($P(RETURN(1.16),U):"MIL SEXUAL TRAUMA",1:"")
 S $P(RETURN(1.17),U,2)=$S($P(RETURN(1.17),U):"COMBAT VET",1:"")
 S $P(RETURN(1.18),U,2)=$S($P(RETURN(1.18),U):"SHAD",1:"")
 ;COMMENTS
 S CNT=0
 S RETURN(10,0)=CNT ;(CNT,GMPORIG(10,0),GMPFLD(10,0))=0
 S FAC=$O(^AUPNPROB(GMPIFN,11,"B",$$GMPVAMC,0)) Q:'FAC 1
 F NIFN=0:0 S NIFN=$O(^AUPNPROB(GMPIFN,11,FAC,11,"B",NIFN)) Q:NIFN'>0  D
 . I $G(GMPLMGR)'="",$P($G(^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0)),U,6)'=+GMPROV Q
 . S CNT=CNT+1,RETURN(10,CNT)=$G(^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0))
 . S $P(RETURN(10,CNT),U,2)=FAC
 S RETURN(10,0)=CNT
 Q 1
 ;
DETAILF(GMPIFN,GMPFLD,ERT) ; Return problem details
 ;
 ; Input   GMPIFN  Pointer to Problem file #9000011
 ;
 ; Output  GMPFLD Array, passed by reference
 ;
 N GMPBUF,FILE,I
 S PROBLEM=9000011
 D GETS^DIQ(PROBLEM,GMPIFN,".01:.03;.05;.06;.08;.12;.13;1.01:1.18","IE","GMPBUF","ERT")
 Q:'$D(GMPBUF(PROBLEM,GMPIFN_",")) 0
 S I=""
 F  S I=$O(GMPBUF(PROBLEM,GMPIFN_",",I)) Q:I=""  D
 . S GMPFLD(I)=GMPBUF(PROBLEM,GMPIFN_",",I,"I")_U_GMPBUF(PROBLEM,GMPIFN_",",I,"E")
 Q 1
 ;
GMPVAMC() ;
 Q +$G(DUZ(2))
 ;
SELCLIN ; SELECT CLINIC
 S GMPCLIN=$O(^SC("B","VISTA HEALTH CARE",""))_"^VISTA HEALTH CARE"
 Q
 ;
SELPN ; SELECT PROVIDER NARRATIVE
 N NMI
 S NMI=$O(^AUTNPOV("B","Hypopituitarism (Diabetes"))
 S GMPOVNAR=$O(^AUTNPOV("B",NMI,""))
 S GMPOVNAR=GMPOVNAR_"^"_^AUTNPOV(GMPOVNAR,0)
 Q
 ;
UNAME() ; USER NAME
 Q $P(^VA(200,DUZ,0),U)
 ;
ADDLIST(RETURN,LSTNAME,CATNAME,LOC) ;
 N NLST,NCAT,ITEM,DIK
 S TARGET="^TMP(""GMPLLST"",$J)"
 S NLST=$$NEWLST(LSTNAME,LOC)
 S NCAT=$$NEWCAT(CATNAME)
 S DIK="^GMPL(125.12,"
 S ITEM="1^33572^Diabetes Insipidus^253.5"
 D NEW(DIK,+NCAT,ITEM)
 S DIK="^GMPL(125.1,"
 S ITEM="1"_U_NCAT_U_"1"
 D NEW(DIK,+NLST,ITEM)
 S RETURN("LST")=NLST
 S RETURN("CAT")=NCAT
 Q
 ;
NEWLST(LNAME,LOC)
 N X,Y,DIC,DIE,DLAYGO,DA,DR,GMPLSLST
 S (X,Y)=LNAME
 S DIC="^GMPL(125,",DIC(0)="L",DIC("DR")=".02////"_DT,DLAYGO=125 K DD,DO
 D FILE^DICN I Y'>0 W !!,"ERROR -- Cannot create new list!",$C(7) Q 0
 S GMPLSLST=$P(Y,U,1,2),DIE=DIC,DA=+Y,DR=".03////"_LOC D ^DIE ; clinic
 Q GMPLSLST
 ;
NEWCAT(HDR) ; Create new group entries in #125.1 and #125.11
 N DIC,DD,DO,X,Y,DIK,ITEM,DLAYGO
 S DIC="^GMPL(125.11,",DIC(0)="L",DIC("DR")="1////"_DT,DLAYGO=125.11
 I $L(HDR),'$D(^GMPL(125.11,"B",$$UP^XLFSTR(HDR))) S X=$$UP^XLFSTR(HDR)
 D FILE^DICN G:Y'>0 NGQ
NGQ S Y=$P(Y,U,1,2)
 Q Y
 ;
NEW(DIK,LIST,ITEM) ; Create new entry in Contents file #125.1 or #125.12
 N I,HDR,LAST,TOTAL,DA
 S HDR=$G(@(DIK_"0)")),LAST=$P(HDR,U,3),TOTAL=$P(HDR,U,4)
 F I=(LAST+1):1 Q:'$D(@(DIK_"I,0)"))
 S DA=I,@(DIK_"DA,0)")=LIST_U_ITEM
 S $P(@(DIK_"0)"),U,3,4)=DA_U_(TOTAL+1)
 D IX1^DIK ; set Xrefs
 Q
 ;
ASSUSR(LIST,USER)
 N DIE,GMPLI,DR
 S DIE="^VA(200,",DR="125.1////"_(+LIST)
 F GMPLI=1:1:$L(USER,U) S DA=$P(USER,U,GMPLI) I DA D ^DIE
 Q
 ;
