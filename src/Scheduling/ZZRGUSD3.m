ZZRGUSD3 ;Unit Tests - Clinic API; 10/26/2012  11:46 AM
 ;;1.0;UNIT TEST;;05/28/2012;
 TSTART
 I $T(EN^XTMUNIT)'="" D EN^XTMUNIT("ZZRGUSD3")
 TROLLBACK
 Q
STARTUP ;
 S DTIME=500,DUZ=1,U="^"
 K XUSER,XOPT
 D LOGON^ZZRGUTCM
 S DT=$P($$HTFM^XLFDT($H),".")
 D SETUP^ZZRGUSDC()
 Q
 ;
SHUTDOWN ;
 Q
 ;
CHKAPP ;
 N RETURN
 S ^DPT(DFN,.35)=DT
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN,.LVL)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: PATDIED")
 D CHKEQ^XTMUNIT(RETURN(0),"PATDIED^PATIENT HAS DIED.^1","Expected error: PATDIED")
 S ^DPT(DFN,.35)="" K RETURN
 S ^SC(SC,"SDPROT")="Y"
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: CLNURGT")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"CLNURGT","Expected error: CLNURGT")
 S ^SC(SC,"SDPRIV",DUZ,0)=DUZ K RETURN
 S TMP=$G(^SC(SC,"ST",DT,1)),^SC(SC,"ST",DT,1)="CANCELLED"
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTCLUV")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTCLUV","Expected error: APTCLUV")
 K RETURN S ^SC(SC,"ST",DT,1)=TMP
 S ^HOLIDAY(DT,0)=DT_U_"Holiday test",$P(^SC(SC,"SL"),U,8)=""
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTSHOL")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTSHOL","Expected error: APTSHOL")
 K RETURN S $P(^SC(SC,"SL"),U,8)="Y"
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,DT+3_".08",LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTEXCD")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTEXCD","Expected error: APTEXCD")
 ;check if patient has an active appointment on the same time
 K RETURN
 S %=$$MAKE^SDMAPI2(.RET,DFN,SC,SD,TYPE,,LEN,NXT,RSN)
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTPAHA")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTPAHA","Expected error: APTPAHA")
 D CHKEQ^XTMUNIT($P(RETURN(0),U,3),2,"Level 2 expected")
 ;check if patient has an active appointment on the same day
 K RETURN
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,DT_".1",LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTPAHA")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTPHSD","Expected error: APTPAHA")
 D CHKEQ^XTMUNIT($P(RETURN(0),U,3),3,"Level 3 expected")
 ;check if patient has an canceled appointment on the same time
 K RETURN
 S $P(^DPT(DFN,"S",SD,0),U,2)="PC"
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTPPCP")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTPPCP","Expected error: APTPPCP")
 D CHKEQ^XTMUNIT($P(RETURN(0),U,3),2,"Level 2 expected")
 ;check if date is prior to patient birth date
 K RETURN,^DPT(DFN,"S")
 S $P(^DPT(DFN,0),U,3)=DT+2
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTPPAB")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTPPAB","Expected error: APTPPAB")
 D CHKEQ^XTMUNIT($P(RETURN(0),U,3),1,"Level 1 expected")
 ;check overbook
 K RETURN
 S $P(^DPT(DFN,0),U,3)=DT-100
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTNOST")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTNOST","Expected error: APTNOST")
 D CHKEQ^XTMUNIT($P(RETURN(0),U,3),1,"Level 1 expected")
 K RETURN S ^XUSEC("SDOB",DUZ)=""
 S %=$$CHKAPP^SDMAPI2(.RETURN,SC,DFN,SD,LEN)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTOVBK")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTOVBK","Expected error: APTOVBK")
 D CHKEQ^XTMUNIT($P(RETURN(0),U,3),2,"Level 2 expected")
 Q
 ;
LSTPATS ;
 N RETURN
 S %=$$LSTPATS^SDMAPI3(.RETURN,"TEST")
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: ")
 D CHKEQ^XTMUNIT(%,1,"Unexpected error: ")
 D CHKEQ^XTMUNIT(+RETURN(0),1)
 D CHKEQ^XTMUNIT(RETURN(1,"ID"),DFN)
 D CHKEQ^XTMUNIT(RETURN(1,"NAME"),PNAME)
 Q
GETPAT ; Get a patient
 K RETURN
 S %=$$GETPAT^SDMAPI3(.RETURN,DFN,.LVL) ; Get a patient
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: PATSENS")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"PATSENS","Expected error: PATSENS")
 K RETURN
 S %=$$GETPAT^SDMAPI3(.RETURN,DFN,1) ; Get a patient
 D CHKEQ^XTMUNIT($P(RETURN("NAME"),U,2),PNAME)
 Q
DEMOG ;
 K RETURN
 S %=$$LSTETNS^SDMAPI3(.RETURN)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: ")
 S ETN=$G(RETURN(1,"ID"))
 K RETURN
 S %=$$SETETN^SDMAPI3(.RETURN,DFN,ETN)
 D CHKEQ^XTMUNIT(^DPT(DFN,.06,1,0),ETN_U_ETN)
 K RETURN
 S %=$$LSTRACES^SDMAPI3(.RETURN)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: ")
 S RAC=$G(RETURN(1,"ID"))
 K RETURN
 S %=$$SETRACE^SDMAPI3(.RETURN,DFN,RAC)
 D CHKEQ^XTMUNIT(^DPT(DFN,.06,1,0),ETN_U_ETN)
 K RETURN
 S %=$$GETPRES^SDMAPI3(.RETURN,DFN) ; Get a patient
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error:")
 D CHKEQ^XTMUNIT(RETURN(1,"ID"),RAC,"Invalid patient race")
 Q
CHKCO ;
 K RETURN
 S %=$$CHKCO^SDMAPI4(.RETURN,DFN,SD)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error:")
 Q
LSTDAYAP ;
 K RETURN
 S SD=$P(DT,".")_".1"
 S %=$$MAKE^SDMAPI2(.RETURN,DFN,SC,SD,TYPE,,LEN,NXT,RSN)
 S %=$$LSTDAYAP^SDMAPI4(.RETURN,DFN)
 D CHKEQ^XTMUNIT(RETURN(SD,"C","CIFN"),SC,"Invalid clinic IFN")
 D CHKEQ^XTMUNIT(RETURN(SD,"C","PATIENT"),PNAME,"Invalid patient")
 D CHKEQ^XTMUNIT(RETURN(SD,"CLINIC"),SC_U_CNAME,"Invalid clinic")
 D CHKEQ^XTMUNIT(RETURN(SD,"STATUS"),U,"Invalid status")
 K RETURN
 S %=$$GETPAPT^SDMAPI4(.RETURN,DFN,SD)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error:")
 D CHKEQ^XTMUNIT(RETURN("CLINIC"),CNAME,"Invalid clinic name")
 D CHKEQ^XTMUNIT(RETURN("CLINIC","I"),SC,"Invalid clinic name")
 D CHKEQ^XTMUNIT(RETURN("DDATE","I"),$P(SD,"."),"Invalid DDATE")
 D CHKEQ^XTMUNIT(RETURN("ENTRY","I"),DUZ,"Invalid user")
 D CHKEQ^XTMUNIT(RETURN("MADEDT","I"),$P(SD,"."),"Invalid made date")
 D CHKEQ^XTMUNIT(RETURN("NEXTA","I"),3,"Invalid next av")
 D CHKEQ^XTMUNIT(RETURN("TYPE","I"),TYPE,"Invalid type")
 K RETURN
 S %=$$GETCAPT^SDMAPI4(.RETURN,DFN,SD)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error:")
 D CHKEQ^XTMUNIT(RETURN("LENGTH"),LEN,"Invalid length")
 D CHKEQ^XTMUNIT(RETURN("ENTRY"),$P(^VA(200,DUZ,0),U),"Invalid user")
 D CHKEQ^XTMUNIT(RETURN("OTHER"),RSN,"Invalid reason")
 D CHKEQ^XTMUNIT(RETURN("PATIENT"),PNAME,"Invalid patient")
 Q
GETPAT2 ;
 N PAT
 S %=$$GETPAT^SDMAPI4(.PAT,DFN)
 D CHKEQ^XTMUNIT(PAT,1,"Unexpected error:")
 D CHKEQ^XTMUNIT(PAT("PATIENT"),PNAME,"Invalid patient name")
 Q
ANCTEST ;
 N RETURN,LAB,EKG,XRAY
 S LAB=$P(DT,".")_".103",XRAY=$P(DT,".")_".11",EKG=$P(DT,".")_".113"
 S %=$$ADDTSTS^SDMAPI4(.RETURN,DFN,SD,.LAB,.XRAY,.EKG)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error:")
 D CHKEQ^XTMUNIT($P(^DPT(DFN,"S",SD,0),U,3,5),LAB_U_XRAY_U_EKG,"Invalid length")
 K RETURN
 S %=$$CHECKO^SDMAPI4(.RETURN,DFN,SD,SC)
 S SDOE=RETURN("SDOE")
 S %=$$ADDTSTS^SDMAPI4(.RETURN,DFN,SD,.LAB,.XRAY,.EKG)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error:")
 D CHKEQ^XTMUNIT($P(^DPT(DFN,"S",SD,0),U,3,5),LAB_U_XRAY_U_EKG,"Invalid length")
 K RETURN
 S %=$$DELTSTS^SDMAPI4(.RETURN,DFN,SD,"","","")
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error:")
 D CHKEQ^XTMUNIT($P(^DPT(DFN,"S",SD,0),U,3,5),U_U,"Invalid length")
 Q
CANDELCO ;
 N CAN
 S ^XUSEC("SD SUPERVISOR",DUZ)=""
 S %=$$CANDELCO^SDMAPI4(.CAN)
 D CHKEQ^XTMUNIT(CAN,1,"Unexpected error: "_$G(CAN(0)))
 K CAN,^XUSEC("SD SUPERVISOR",DUZ)
 S %=$$CANDELCO^SDMAPI4(.CAN)
 D CHKEQ^XTMUNIT(CAN,0,"Expected error: APTCOSU")
 D CHKEQ^XTMUNIT($P(CAN(0),U),"APTCOSU","Expected error: APTCOSU")
 N OE
 S %=$$DELCODT^SDMAPI4(.OE,SDOE)
 D CHKEQ^XTMUNIT(OE,1,"Unexpected error: "_$G(OE(0)))
 Q
REQESTS ;
 N RETURN,SCODE
 S TC=$P(^SC(SC,0),U,7),$P(^SC(SC,0),U,7)=105
 S CONS=$$ADDREQ^ZZRGUSDC(SD,DFN,105)
 S %=$$GETAPCNS^SDCAPI1(.RETURN,DFN,105)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: "_$G(RETURN(0)))
 ;autorebook
 K RETURN
 S %=$$AUTOREB^SDCAPI1(.RETURN,SC,SD,CONS,1)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: "_$G(RETURN(0)))
 S $P(^SC(SC,0),U,7)=TC
 Q
CANAPP ;
 K RETURN,^SC(SC,"S"),^DPT(DFN,"S")
 S SD1=$P(DT,".")_".13"
 ;reactivate cancelled appointment
 S %=$$MAKE^SDMAPI2(.RETURN,DFN,SC,SD1,TYPE,,LEN,NXT,RSN)
 S %=$$CANCEL^SDMAPI2(.RETURN,DFN,SC,SD1,"PC",RSN,"Cancellation test remarks")
 S %=$$MAKE^SDMAPI2(.RETURN,DFN,SC,SD1,TYPE,,LEN,NXT,RSN,,,,,,,.LVL)
 D CHKEQ^XTMUNIT(RETURN,0,"Expected error: APTPPCP")
 D CHKEQ^XTMUNIT($P(RETURN(0),U),"APTPPCP","Expected error: APTPPCP")
 K RETURN
 S %=$$MAKE^SDMAPI2(.RETURN,DFN,SC,SD1,TYPE,,LEN,NXT,RSN,,,,,,CONS,1)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: "_$G(RETURN(0)))
 ;Cancel existing appointment on same time
 K RETURN
 S %=$$MAKE^SDMAPI2(.RETURN,DFN,SC,SD1,TYPE,,LEN,NXT,RSN,,,,,,CONS,1)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: "_$G(RETURN(0)))
 K RETURN
 S SD1=$P(DT,".")_".22"
 S %=$$MAKE^SDMAPI2(.RETURN,DFN,SC,SD1,TYPE,,LEN,NXT,RSN,,,,,,CONS,1)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: "_$G(RETURN(0)))
 K RETURN
 S SD1=$P(DT,".")_".23"
 S %=$$MAKE^SDMAPI2(.RETURN,DFN,SC,SD1,TYPE,,60,NXT,RSN,,,,,,CONS,1)
 D CHKEQ^XTMUNIT(RETURN,1,"Unexpected error: "_$G(RETURN(0)))
 Q
XTENT ;
 ;;CHKAPP;Check make apt
 ;;LSTPATS;Get patients
 ;;GETPAT;Get a patient
 ;;DEMOG;Patient details
 ;;CHKCO;Check in check out
 ;;LSTDAYAP;List all day active appointment
 ;;GETPAT2;Get patient
 ;;ANCTEST;Add/Delete tests
 ;;CANDELCO;Can delete check out?
 ;;REQESTS;Consult request apt
 ;;CANAPP;Un cancel
